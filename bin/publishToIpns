#!/usr/bin/env coffee

require('dotenv').load()
{d, json, log, type} = require 'lightsaber'
{exec, exit} = require 'shelljs'
{clone, defaults, merge, isEmpty} = require 'lodash'
chalk = require 'chalk'

IPFS = "node_modules/.bin/ipfs --config tmp/ipfs"
PEER_ID = process.env.PEER_ID or throw new Error "Please set env var PEER_ID"
PRIVATE_KEY = process.env.PRIVATE_KEY or throw new Error "Please set env var PRIVATE_KEY"

daemon = null

main = ->
  daemon = run "#{IPFS} init --force --empty-repo"
  daemon = run "#{IPFS} config Identity.PeerID #{PEER_ID}"
  daemon = run  "#{IPFS} config Identity.PrivKey '#{PRIVATE_KEY}'", quiet: true

  daemon = run "#{IPFS} daemon", async: true
  try
    daemon.stdout.on 'data', (data) ->
      if data.match /Daemon is ready/
        publishToIpns()
  catch error
    killDaemon()
    throw error

publishToIpns = ->
  ipfsHash = run("#{IPFS} add -r -q public | tail -n 1").output.trim()
  run "#{IPFS} name publish #{ipfsHash}"
  fetch "http://localhost:8080/ipfs/#{ipfsHash}"
  fetch "https://ipfs.io/ipfs/#{ipfsHash}"
  fetch "https://ipfs.io/ipns/#{PEER_ID}"
  killDaemon()

fetch = (url) ->
  run "
    wget
      --directory-prefix=tmp/wget
      --force-directories
      --page-requisites
      --timeout=600
      --wait=1
      --tries=60
      --waitretry=60
      #{url}
    "

killDaemon = -> run "kill -9 #{daemon.pid}"

run = (cmd, options={}) ->
  originalOptions = clone options
  defaults options,
    relaxed: false
    quiet: false
    quietCommand: options.quiet ? false
    quietResponse: options.quiet ? false

  unless options.quietCommand
    log chalk.green "\n==> #{cmd}   #{if isEmpty(originalOptions) then '' else '# '+json(originalOptions)}"

  result = exec cmd, merge(options, quiet: options.quietResponse)

  exitCode = result.code
  if type(exitCode) isnt 'undefined' and exitCode isnt 0
    console?.error "COMMAND FAILED: #{ json {exitCode} }"
    exit(exitCode) unless options.relaxed
  result

main()
